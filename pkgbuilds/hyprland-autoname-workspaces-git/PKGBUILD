# Maintainer: Cyrinux <pkgbuilds AT levis DOT name>

_pkgname=hyprland-autoname-workspaces
pkgname="${_pkgname}-git"
pkgver=1
pkgrel=1
pkgdesc='Hyprland autoname workspaces'
arch=('x86_64')
url="https://github.com/hyprland-community/${_pkgname}"
license=('ISC')
depends=('otf-font-awesome' 'hyprland' 'systemd-libs')
makedepends=('cargo' 'git' 'clang' 'gcc' 'pkgconf')
conflicts=("hyprland-autoname-workspaces" "hyprland-autoname-workspaces-bin")
provides=("hyprland-autoname-workspaces")
source=("git+https://github.com/hyprland-community/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    git describe --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

check() {
    cd "${_pkgname}"
    make test
}

build() {
    cd "${_pkgname}"
    make build
}

package() {
    cd "${_pkgname}"
    make install DESTDIR="${pkgdir}"
}
