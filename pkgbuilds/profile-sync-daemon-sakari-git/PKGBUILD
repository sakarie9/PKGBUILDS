# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>

_pkgname=profile-sync-daemon
pkgname=$_pkgname-sakari-git
pkgver=1
pkgrel=1
pkgdesc="Symlinks and syncs browser profile dirs to RAM"
arch=(any)
url="https://github.com/graysky2/profile-sync-daemon"
license=(MIT)
depends=(bash findutils procps-ng rsync systemd)
optdepends=('zsh-completions: for completion when using zsh')
conflicts=(profile-sync-daemon)
install=$_pkgname.install
source=(git+$url.git)
sha512sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$_pkgname"
  make
}

package() {
  make DESTDIR="$pkgdir" install -C $_pkgname
  install -vDm 644 $_pkgname/MIT -t "$pkgdir/usr/share/licenses/$_pkgname/"
  install -vDm 644 $_pkgname/README.md -t "$pkgdir/usr/share/doc/$_pkgname/"
}
