# Maintainer: FabioLolix
# Maintainer: Ã©clairevoyant
# Contributor: ThatOneCalculator <kainoa at t1c dot dev>

pkgname=hyprland-opti-git
pkgver=1
pkgrel=1
pkgdesc="A dynamic tiling Wayland compositor based on wlroots that doesn't sacrifice on its looks."
arch=(x86_64)
url="https://github.com/hyprwm/Hyprland"
license=('BSD-3-Clause')
depends=(
  cairo
  gcc-libs
  glib2
  glibc
  glslang
  hyprcursor
  hyprlang
  libdisplay-info
  libdrm
  libglvnd
  libinput
  libliftoff
  libx11
  libxcb
  libxcomposite
  libxfixes
  libxkbcommon
  libxrender
  opengl-driver
  pango
  pixman
  seatd
  systemd-libs
  tomlplusplus
  wayland
  wayland-protocols
  xcb-proto
  xcb-util
  xcb-util-errors
  xcb-util-keysyms
  xcb-util-renderutil
  xcb-util-wm
  xorg-xinput
  xorg-xwayland
)
depends+=(libdisplay-info.so)
makedepends=(
  cmake
  gdb
  git
  meson
  ninja
  pkgconf
  xorgproto
  hyprwayland-scanner
)
provides=("hyprland=${pkgver%%.r*}")
conflicts=(hyprland)
source=("git+https://github.com/hyprwm/Hyprland.git"
  "git+https://github.com/hyprwm/wlroots-hyprland.git"
  "git+https://github.com/hyprwm/hyprland-protocols.git"
  "git+https://github.com/canihavesomecoffee/udis86.git"
  "git+https://github.com/wolfpld/tracy.git")
b2sums=('SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP')

prepare() {
  cd Hyprland
  rm -rf subprojects/{wlroots-hyprland,hyprland-protocols}
  git submodule init
  git config submodule.subprojects/wlroots-hyprland.url "${srcdir}/wlroots-hyprland"
  git config submodule.subprojects/hyprland-protocols.url "${srcdir}/hyprland-protocols"
  git config submodule.subprojects/udis86.url "${srcdir}/udis86"
  git config submodule.subprojects/tracy.url "${srcdir}/tracy"
  git -c protocol.file.allow=always submodule update
}

pkgver() {
  git -C Hyprland describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
  # optimize
  export CFLAGS="${CFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS/} -O3 -flto -march=x86-64-v3"
  export CXXFLAGS="${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS/} -O3 -flto -march=x86-64-v3"

  cd Hyprland
  mkdir -p build && cd build
  cmake -G Ninja -DCMAKE_BUILD_TYPE=None -DCMAKE_SKIP_RPATH=ON -DCMAKE_INSTALL_PREFIX=/usr ..
  ninja
}

package() {
  cd Hyprland

  meson install -C subprojects/wlroots-hyprland/build --destdir "${pkgdir}/tmpwlr"
  install -Dm755 build/Hyprland -t "${pkgdir}/usr/bin/"

  pushd "${pkgdir}/usr/bin"
  ln -sf Hyprland hyprland
  popd

  install -Dm755 build/{hyprctl/hyprctl,hyprpm/hyprpm} -t "${pkgdir}/usr/bin/"
  install -Dm644 assets/*.png -t "${pkgdir}/usr/share/hyprland/"

  install -Dm644 example/hyprland.desktop -t "${pkgdir}/usr/share/wayland-sessions"
  install -Dm644 example/hyprland.conf -t "${pkgdir}/usr/share/hyprland"

  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm644 subprojects/wlroots-hyprland/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-wlroots"
  install -Dm644 subprojects/udis86/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-udis86"

  install -Dm644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
  install -Dm644 "${pkgdir}/tmpwlr/usr/local/lib/libwlroots.a" -t "${pkgdir}/usr/lib/"
  install -d ${pkgdir}/usr/include/hyprland/{protocols,wlroots-hyprland}

  for cmd in hyprctl hyprpm; do
    install -Dm644 "${cmd}/${cmd}.bash" "${pkgdir}/usr/share/bash-completion/completions/${cmd}"
    install -Dm644 "${cmd}/${cmd}.zsh" "${pkgdir}/usr/share/zsh/site-functions/_${cmd}"
    install -Dm644 "${cmd}/${cmd}.fish" -t "${pkgdir}/usr/share/fish/vendor_completions.d/"
  done

  cp -R src "${pkgdir}/usr/include/hyprland/"
  cp -R ${pkgdir}/tmpwlr/usr/local/include/* "${pkgdir}/usr/include/hyprland/wlroots-hyprland/"

  find "${pkgdir}/usr/include/hyprland/" -type f ! -name '*.h*' -delete
  rm -rf "${pkgdir}/tmpwlr/"

  cp protocols/*.h* "${pkgdir}/usr/include/hyprland/protocols/"
  install -Dm644 build/hyprland.pc -t "${pkgdir}/usr/share/pkgconfig/"
}
