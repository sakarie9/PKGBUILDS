# Maintainer: FabioLolix
# Maintainer: Ã©clairevoyant
# Contributor: ThatOneCalculator <kainoa at t1c dot dev>

pkgname=hyprland-opti-git
pkgver=1
pkgrel=1
pkgdesc="A dynamic tiling Wayland compositor based on wlroots that doesn't sacrifice on its looks."
arch=(x86_64)
url="https://github.com/hyprwm/Hyprland"
license=(BSD)
depends=(cairo
  gcc-libs
  glib2
  glibc
  glslang
  libdisplay-info
  libdrm
  libglvnd
  libinput
  libliftoff
  libx11
  libxcb
  libxcomposite
  libxfixes
  libxkbcommon
  libxrender
  opengl-driver
  pango
  pixman
  polkit
  seatd
  systemd-libs
  wayland
  wayland-protocols
  xcb-proto
  xcb-util
  xcb-util-errors
  xcb-util-keysyms
  xcb-util-renderutil
  xcb-util-wm
  xorg-xinput
  xorg-xwayland)
depends+=(libdisplay-info.so)
makedepends=(cmake
  gdb
  git
  jq
  meson
  ninja
  pkgconf
  xorgproto)
provides=("hyprland=${pkgver%%.r*}")
conflicts=(hyprland)
source=("git+https://github.com/hyprwm/Hyprland.git"
  "git+https://gitlab.freedesktop.org/wlroots/wlroots.git"
  "git+https://github.com/hyprwm/hyprland-protocols.git"
  "git+https://github.com/canihavesomecoffee/udis86.git"
  "git+https://github.com/wolfpld/tracy.git")
b2sums=('SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP')

prepare() {
  cd Hyprland
  git submodule init
  git config submodule.wlroots.url "$srcdir/wlroots"
  git config submodule.subprojects/hyprland-protocols.url "$srcdir/hyprland-protocols"
  git config submodule.subprojects/udis86.url "$srcdir/udis86"
  git config submodule.subprojects/tracy.url "$srcdir/tracy"
  git -c protocol.file.allow=always submodule update

  # optimize
  export CFLAGS="${CFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS/} -O3 -flto -march=x86-64-v3"
  export CXXFLAGS="${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS/} -O3 -flto -march=x86-64-v3"

  git -C subprojects/wlroots reset --hard
  patch -d subprojects/wlroots -Np1 < subprojects/packagefiles/wlroots-meson-build.patch
}

# pkgver() {
# 	cd Hyprland
# 	(
# 		set -o pipefail
# 		git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
# 			printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
# 	)
# }

pkgver() {
  git -C Hyprland describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
  cd Hyprland

  meson setup build \
    --prefix     /usr \
    --libexecdir lib \
    --sbindir    bin \
    --buildtype  release \
    --wrap-mode  nodownload \
    -D           b_lto=true \
    -D           b_pie=true \
    -D           default_library=shared \
    -D           xwayland=enabled

  ln -sf wlroots build/subprojects/wlroots/include/wlr
  meson compile -C build
}

package() {
  cd Hyprland

  meson install -C build \
    --destdir "$pkgdir" \
    --skip-subprojects hyprland-protocols

  rm -rf "$pkgdir/usr/include/hyprland/wlroots/wlr"
  ln -sf . "$pkgdir/usr/include/hyprland/wlroots/wlr"
  # resolve conflicts with system wlr
  rm -f "$pkgdir/usr/lib/libwlroots.so"
  rm -rf "$pkgdir/usr/lib/pkgconfig"
  # FIXME: remove after xdg-desktop-portal-hyprland disowns hyprland-portals.conf
  rm -rf "$pkgdir/usr/share/xdg-desktop-portal"

  # license
  install -Dm0644 -t "$pkgdir/usr/share/licenses/${pkgname}" LICENSE
}
